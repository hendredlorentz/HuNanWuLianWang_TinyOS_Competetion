2020.11.11练习题

### 找出发包的三个结点号

#### 题目

给定 12个传感器节点，除左下角 0 号节点外，其他 11个节点的 ID 随机产生，右上角节点，左上角和右下角的 3 个节点被称为“发包者”，他们==以 1 秒为周期广播==，在 ==26 信道==以==数据包[ID,50]==的形式以 ==AMType 为 0x22== 的接口广播信息，发送功率为最小值 1。 参赛者对除三个“发包者”节点之外的 9 个节点编写程序，==找出全部三个“发包者”的节点ID==，所有结点打印“发包者”的结点号。

- “发包者”节点程序由大赛提供；

#### 评分规则

本题满分 50 分，按照三个部分进行评分：第一部分为正确性（占30 分），第二部分为完成时间（占 10 分），第三部分为网络开销（占

10 分）。在正确性得分不为 0 分的情况下，才会进行第二和第三部分的评分，如果正确性得分为 0 分，则第二和第三部分不进行评分，直接

记为 0 分。

正确性部分根据所算结果进行打分，正确性越高，分数越高；

完成时间部分根据算出结果的时间进行打分，完成时间越短，分数越高；

网络开销部分根据比赛过程中==整个网络中所有节点发送数据包总数目==进行打分，发送数据包总数目越少，分数越高。

**第一部分“正确性”得分：** 

正确找到全部 3 个“发包者”ID 可得 30 分 ；找到其中任意 2 个“发包者”ID 可得 20 分找到其中任意 1 个“发包者”ID 可得 10 分 ；其余情况判分为 0 分

**第二部分“完成时间”得分：** 

计算公式：（1-（完成时间（毫秒）/10000（毫秒）））*10

注：如果完成时间超过总时间或第一部分得分为 0 分，则“完成时间”得分计为 0 分。

**第三部分“网络开销”得分：** 

计算公式：第四届全国高校物联网应用创新大赛挑战赛决赛赛题（1-（全网开销个数/1000））*10

注：如果全网开销个数超过 1000 或第一部分得分为 0 分，则“网络开销”得分计为 0 分。

#### 解题思路

1. “发包者”结点初始只发送一个包；
2. 所有结点创建数组node[3]进行保存“发包者”的结点号；
3. 结点接收到包，判断是否已保存该结点，未保存就保存结点号并转发，已保存就不转发；
4. 除“发包者”的其他结点保存3个结点号后进行打印；

#### 结果展示

![image-20201111143217335](C:\Users\HP\AppData\Roaming\Typora\typora-user-images\image-20201111143217335.png)

#### 源代码

- Makefile

  ```nesc
  COMPONENT=Exercise4AppC
  CFLAGS += -DCC2420_DEF_CHANNEL=14
            -DCC2420_DEF_RFPOWER=31
  
  CFLAGS += -I$(TOSDIR)/lib/printf
  
  include $(MAKERULES)
  ```

- Exercise4.h

  ```nesc
  #include "printf.h"
  
  #ifndef RADIO_COUNT_TO_LEDS_H
  #define RADIO_COUNT_TO_LEDS_H
  
  //无线通信包
  typedef nx_struct radio_msg {
    nx_uint16_t nodeid;
  }radio_msg_t;
  
  enum {
    AM_RADIO_COUNT_MSG = 6,
  };
  
  #endif
  ```

- Exercise4AppC.nc

  ```nesc
  #include "printf.h"
  #include "Exercise4.h"
  
  /*
   *需要用到的模块：
   *-----无线通信：
   *无线通信发包（所有结点）
   *无线通信收包（所有结点）
   */
  
  configuration Exercise4AppC {}
  implementation {
    
    //main
    components MainC, Exercise4C as App, LedsC;
    App.Boot -> MainC.Boot;
    App.Leds -> LedsC;
    
    //timer
    components new TimerMilliC() as Timer0;
    components new TimerMilliC() as Timer1;
    App.Timer0 -> Timer0;
    App.Timer1 -> Timer1;
    
    //radio：无线通信
    components ActiveMessageC;
    components new AMSenderC(AM_RADIO_COUNT_MSG);
    components new AMReceiverC(AM_RADIO_COUNT_MSG);
    App.RadioPacket -> AMSenderC;
    App.RadioReceive -> AMReceiverC;
    App.RadioSend -> AMSenderC;
    App.RadioControl -> ActiveMessageC;
   
    //printf
    components PrintfC;
    components SerialStartC;
  }
  ```

- Exercise4C.nc

  ```
  #include "Timer.h"
  #include "printf.h"
  #include "Exercise4.h"
   
  module Exercise4C @safe() {
    uses {
      interface Leds;
      interface Boot;
  	interface Timer<TMilli> as Timer0;
      interface Timer<TMilli> as Timer1;  
  	
  	//radio
  	interface SplitControl as RadioControl;
      interface Receive as RadioReceive;
      interface AMSend as RadioSend;
      interface Packet as RadioPacket;
    }
  }
  
  implementation {
    
    message_t radiopacket;
  
    uint16_t node[3];
    // uint16_t node[3]={101,};
    
    int count_send=0;
    int count_node=0;
    
    bool sendBusy = FALSE;  //发完一个包之后才会发下一个包
    bool had_flag = FALSE;  //是否已保存结点，TRUE表示已保存该结点，FALSE表示没有
    
    void sendMsg();
  
    event void Boot.booted() {
      //开启无线通信
      call RadioControl.start();
    }
   
    //开启无线通信成功
    event void RadioControl.startDone(error_t err) {
      if (err != SUCCESS){
  		call RadioControl.start();
      }else{
  	      
  		int i=0;
  		for(i=0;i<3;i++){
  			node[i]=101;
  		}
  		
  		//只有0，8，11号结点发包
  		if(TOS_NODE_ID==0||TOS_NODE_ID==8||TOS_NODE_ID==11){
  			call Timer0.startOneShot(1000+TOS_NODE_ID*20);  //每个结点发送一次包
  		}else if(TOS_NODE_ID!=0&&TOS_NODE_ID!=8&&TOS_NODE_ID!=11){
              call Timer1.startOneShot(5000+TOS_NODE_ID*20);
          }
  	}
    }
    
    event void RadioControl.stopDone(error_t err) {
    }
    
    event void Timer0.fired() {
        radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
        
        rcm_send->nodeid=TOS_NODE_ID;
        
        if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {                 
                    call Leds.led1On();
                    sendBusy = TRUE;
        }
    }
    
    event void Timer1.fired() {
        int i=0;
       
        if(count_node==3){
  		    printf("Data");
  		    for(i=0;i<3;i++){
  				printf(" %u",node[i]);
  			}
  			printf("\n");
  			printfflush();
  			count_node++;
  			printf("send_count:%u\n",count_send);
  			printfflush();
  	  }
    }
    
    //发送无线通信数据包结束
    event void RadioSend.sendDone(message_t* bufPtr, error_t error) {
        if (error == SUCCESS) {  
            sendBusy = FALSE;     
      }
    }
  
    //接收到无线通信数据包
    /*
    接收到无线通信数据包，判断里面是否有未保存的数据结点
    如果有就需要转发该包，保存数据完毕后，将自己结点保存的数组作为包数据发出去
    如果没有则不转发包
    */
    event message_t* RadioReceive.receive(message_t* bufPtr, void* payload, uint8_t len) {
      if (len != sizeof(radio_msg_t)) {return bufPtr;}
      else {
  		if(TOS_NODE_ID!=0&&TOS_NODE_ID!=8&&TOS_NODE_ID!=11){
  			int i=0;
  			radio_msg_t* rcm_receive = (radio_msg_t*)payload;
  			
  			radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
  		   
  			call Leds.led2On();
  			had_flag=FALSE; 
              
  			for(i=0;i<3;i++){
  				if(rcm_receive->nodeid==node[i]){
  					had_flag=TRUE;
  					break;
  				}
  			}
  			
  			if(!had_flag){
  				node[count_node]=rcm_receive->nodeid;
  				
  				count_node++;
  				
  				rcm_send->nodeid=rcm_receive->nodeid;
  				
  				if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {
  					  count_send++;
  					  call Leds.led0On();
  
  			  }
  			}
              
  		}
      }   
      return bufPtr;
    }
  }
  ```

  