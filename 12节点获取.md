### 第三届决赛赛题3

------

#### 题目内容

![image-20201109155728902](C:\Users\HP\AppData\Roaming\Typora\typora-user-images\image-20201109155728902.png)

如图所示，平台12个节点ID号随机（介于0-100），参赛队需==编写12个节点的代码==，完成节点间的信息传递，使得==所有节点能获取到当前网络中存在的节点号==，并在10秒内，==所有节点打印输出当前存在的12个节点的ID号==。

1）代码烧录完毕10秒后停止评分；

2）使用Printf打印结果时，需加上换行符“\n”，以表明语句的结束；

3）网络开销指的是12个节点的无线发包总和；

#### 格式说明

==输出格式为：标志位（Data）+ 12个ID号（10进制）==

例：如果12个节点号为 1 2 3 4 5 7 9 100 101 102 103 104；

正确的输出结果为：==Data 1 2 3 4 5 7 9 100 101 102 103 104==

其中ID号不分顺序，重复的、错误的不扣分，ID号位可多可少，但程序只会取该节点输出的包含标志位Data的最后一行前的12个ID位作为判分依据；

如 Data 1 3 2 4 5 9 7 100 101 102 103 104 （正确12个）

如 Data 1 2 3 4 5 7 9 9 100 101 102 103 104（正确11个，104不得分）

如 Data 1 2 3 4 5 5 7 8 8（正确6个）

#### 计分规则

本赛题满分为40分，其中正确性得分24分，开销得分16分；

==正确性得分==：共24分，确性得分大于18分才可以获取开销分；

​    ![img](file:///C:\Users\HP\AppData\Local\Temp\ksohtml18924\wps2.jpg)    m为12个节点输出正确ID的总和

==开销得分==：共16分，开销低于40得分16分，大于200得分0分，公式如下：

​    ![img](file:///C:\Users\HP\AppData\Local\Temp\ksohtml18924\wps3.jpg)  n为开销

#### 评分步骤

烧录Null程序，清空节点状态；

编译烧录参赛队代码（所有节点）；

对12个节点进行Printf监听并开始计时；

10秒后停止评分，核实结果，计算出得分；

#### 分析题目+解题思路

- 题目编写12个结点的代码，且12个结点号随机；
- 12个结点需要打印所有12个结点的结点号；
- 每个结点设置一个数组保存已收到的结点号数据，数组第0位保存自己的结点号；
- 每个结点初始的时候会发送1次包，之后的发包数根据收到的包进行判断是否转发；
- 每个结点发送的数据包中包含一个数组，数组内容是该结点已收到的结点号信息；
- 结点收到数据包后进行判断，收到的数据包中是否存在自己未保存的结点号，如果有则会进行发包，并保存未保存的结点号；如果没有则不进行发包
- 对结点收到的结点号进行计数count_node，初始值为1（因为保存自己的结点号在0号位），在结点收包程序中进行判断，如果count_node==12,则表示收到所有结点号，进行打印十二个结点的结点号，打印结束进行count_node++，这样保证打印语句只有1句。

#### 结果展示

#### 重要代码实现

- 所有结点初始发送一个包

  ```nesc
  //1.开启无线通信成功触发
    event void RadioControl.startDone(error_t err) {
      if (err != SUCCESS){
  		call RadioControl.start();
      }else{
  	    
          //初始化数组里的第一个数据为自己的结点号，其他数据为-1  
  		int i=0;
          node[0]=TOS_NODE_ID;
  		for(i=1;i<12;i++){
  			node[i]=101;
  		}
          call Timer0.startPeriodic(5000+TOS_NODE_ID*20);  //每个结点初始发送一次包
  	}
    }
    
    event void RadioControl.stopDone(error_t err) {
    }
    
  //2.定时器Timer0触发事件
    event void Timer0.fired() {
        sendMsg();
    }
    
  //3.发送无线通信数据包
    void sendMsg(){
        int i=0;
        //创建无线通信数据包
        radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
        
        //为包里的数据赋值
        for(i=0;i<12;i++)
            rcm_send->node[i]=node[i];
        
        //发送无线通信数据包
        if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {
             count_send++;
             call Leds.led2On();
             sendBusy = TRUE;
        }
    }
  ```

- 结点收到包，判断数据包中是否有未保存的结点号，如果存在未保存的结点号，进行发包；判断是否收全12个结点的结点号

  ```nesc
  event message_t* RadioReceive.receive(message_t* bufPtr, void* payload, uint8_t len) {
      if (len != sizeof(radio_msg_t)) {return bufPtr;}
      else {        
          //变量定义
  		int i=0,j=0,temp=0;
  		radio_msg_t* rcm_receive = (radio_msg_t*)payload;
          
          radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
          
          //收到无线通信数据包，灯0亮
          call Leds.led1On();
          
          temp=0;//临时计数，收到包中有几个不一样的结点值
          
          //收到的包中的12个结点号与已收到结点号作比较，
          for(i=0;i<12;i++){
             
             if(rcm_receive->node[i]==101){
                      break;
              }
              had_flag=FALSE;
              for(j=0;j<count_node;j++){
                  //如果已经保存过该结点，设置had_flag为TRUE，跳出循环
                  if(rcm_receive->node[i]==node[j]){  
                      had_flag=TRUE;
                      break;
                  }
              }
              
              //如果没有被保存过
              if(!had_flag){
                  temp++;  //临时计数，对收到包中 存在没有保存的 结点号的计数
                  node[count_node-1+temp]=rcm_receive->node[i];  //将没有保存的结点号保存在自己的数组中
                  break;
              }
          }
          
          //如果存在没有保存过的结点，进行包的转发
          if(temp>0){
              
              count_node+=temp;
              
              for(i=0;i<12;i++)
                  rcm_send->node[i]=node[i];
              
              if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {
                    count_send++;
  
                    call Leds.led0On();
  
            }
          }
         
         //如果保存了12个结点的结点号，打印结点号数据
         if(count_node==12){
              printf("Data");
              for(i=0;i<12;i++){
  			    printf(" %u",node[i]);
  		    }
              printf("\n");
              printfflush();
              count_node++;
              printf("node: %u,send_count:%u\n",TOS_NODE_ID,count_send);
              printfflush();
          }
      }   
      return bufPtr;
    }
  ```

#### 源码

- Makefile

  ```nesc
  COMPONENT=Exercise3AppC
  CFLAGS += -DCC2420_DEF_CHANNEL=14
            -DCC2420_DEF_RFPOWER=31
  
  CFLAGS += -I$(TOSDIR)/lib/printf
  
  include $(MAKERULES)
  ```

- Exercise3.h

  ```nesc
  #include "printf.h"
  
  #ifndef RADIO_COUNT_TO_LEDS_H
  #define RADIO_COUNT_TO_LEDS_H
  
  //无线通信包
  typedef nx_struct radio_msg {
    nx_uint16_t node[12];
  }radio_msg_t;
  
  enum {
    AM_RADIO_COUNT_MSG = 6,
  };
  
  #endif
  
  ```

- Exercise3AppC.nc

  ```nesc
  #include "printf.h"
  #include "Exercise3.h"
  
  /*
   *需要用到的模块：
   *-----无线通信：
   *无线通信发包（所有结点）
   *无线通信收包（所有结点）
   */
  
  configuration Exercise3AppC {}
  implementation {
    
    //main
    components MainC, Exercise3C as App, LedsC;
    App.Boot -> MainC.Boot;
    App.Leds -> LedsC;
    
    //timer
    components new TimerMilliC();
    App.Timer0 -> TimerMilliC;
    
    //radio：无线通信
    components ActiveMessageC;
    components new AMSenderC(AM_RADIO_COUNT_MSG);
    components new AMReceiverC(AM_RADIO_COUNT_MSG);
    App.RadioPacket -> AMSenderC;
    App.RadioReceive -> AMReceiverC;
    App.RadioSend -> AMSenderC;
    App.RadioControl -> ActiveMessageC;
   
    //printf
    components PrintfC;
    components SerialStartC;
  }
  
  ```

- Exercise3C.nc

  ```nesc
  #include "Timer.h"
  #include "Exercise3.h"
  #include "printf.h"
   
  module Exercise3C @safe() {
    uses {
      interface Leds;
      interface Boot;
  	interface Timer<TMilli> as Timer0;
  	
  	//radio
  	interface SplitControl as RadioControl;
      interface Receive as RadioReceive;
      interface AMSend as RadioSend;
      interface Packet as RadioPacket;
    }
  }
  
  implementation {
  
    message_t radiopacket;
  
    uint16_t node[12];
    
    int count_send=0;
    int count_node=1;
    
    bool sendBusy = FALSE;  //发完一个包之后才会发下一个包
    bool had_flag = FALSE;  //是否已保存结点，TRUE表示已保存该结点，FALSE表示没有
    
    void sendMsg();
  
    event void Boot.booted() {
      //开启无线通信
      call RadioControl.start();
    }
   
    //开启无线通信成功
    event void RadioControl.startDone(error_t err) {
      if (err != SUCCESS){
  		call RadioControl.start();
      }else{
  	    
          //初始化数组里的第一个数据为自己的结点号，其他数据为-1  
  		int i=0;
          node[0]=TOS_NODE_ID;
  		for(i=1;i<12;i++){
  			node[i]=101;
  		}
          call Timer0.startPeriodic(5000+TOS_NODE_ID*20);  //每个结点发送一次包
  	}
    }
    
    event void RadioControl.stopDone(error_t err) {
    }
    
    event void Timer0.fired() {
        sendMsg();
    }
    
    
    void sendMsg(){
        int i=0;
        radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
        
        for(i=0;i<12;i++)
            rcm_send->node[i]=node[i];
        
        if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {
                    count_send++;
                    
                    call Leds.led2On();
  
                    sendBusy = TRUE;
        }
    }
    
    //发送无线通信数据包结束
    event void RadioSend.sendDone(message_t* bufPtr, error_t error) {
        if (error == SUCCESS) {  
            sendBusy = FALSE;     
      }
    }
  
    //接收到无线通信数据包
    /*
    接收到无线通信数据包，判断里面是否有未保存的数据结点
    如果有就设置转发包的flag为TRUE，保存数据完毕后，将自己结点保存的数组作为包数据发出去
    如果没有则不转发包
    */
    event message_t* RadioReceive.receive(message_t* bufPtr, void* payload, uint8_t len) {
      if (len != sizeof(radio_msg_t)) {return bufPtr;}
      else {        
          
  		int i=0,j=0,temp=0;
  		radio_msg_t* rcm_receive = (radio_msg_t*)payload;
          
          radio_msg_t* rcm_send = (radio_msg_t*)call RadioPacket.getPayload(&radiopacket, sizeof(radio_msg_t));
         
          call Leds.led1On();
          
          temp=0;//临时计数，收到包中有几个不一样的结点值
          
          for(i=0;i<12;i++){
             
             if(rcm_receive->node[i]==101){
                      break;
              }
              had_flag=FALSE;
              for(j=0;j<count_node;j++){
                  if(rcm_receive->node[i]==node[j]){ 
                      had_flag=TRUE;
                      break;
                  }
              }
              
              if(!had_flag){
                  temp++;  //临时计数，对收到包中 存在没有保存的 结点号的计数
                  node[count_node-1+temp]=rcm_receive->node[i];  //将没有保存的结点号保存在自己的数组中
                  break;
              }
          }
          
          //如果转发标志位为真，进行发包操作
          if(temp>0){
              
              count_node+=temp;
              
              for(i=0;i<12;i++)
                  rcm_send->node[i]=node[i];
              
              if (call RadioSend.send(AM_BROADCAST_ADDR, &radiopacket, sizeof(radio_msg_t)) == SUCCESS) {
                    count_send++;
                    call Leds.led0On();
            }
          }
          
         if(count_node==12){
              printf("Data");
              for(i=0;i<12;i++){
  			    printf(" %u",node[i]);
  		    }
              printf("\n");
              printfflush();
              count_node++;
              printf("node: %u,send_count:%u\n",TOS_NODE_ID,count_send);
              printfflush();
          }
      }   
      return bufPtr;
    }
  }
  ```

  